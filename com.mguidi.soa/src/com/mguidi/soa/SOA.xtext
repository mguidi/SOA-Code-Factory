grammar com.mguidi.soa.SOA with org.eclipse.xtext.common.Terminals

generate soa "http://www.mguidi.com/soa/SOA"

Architecture:
	'@applicationId' ':' name=QualifiedName
	module=Module
;

Module:
	'@moduleName' ':' name=ID
	'@version' ':' version=Version
	imports+=Import*
	model=Model?
	exceptions=Exceptions?
	services+=Service*
	event=Event?
;

QualifiedName:
	ID ('.' ID)*;
	
Version:
	INT '.' INT '.' INT ('-' ID)?
;
	
Import:
  '@import' ':' importedNamespace = QualifiedName
;
		
/************************ Model definition *************************/
Model:
	'@model' '{' entities+=Entities+ '}'
;

Comment:
	value=COMMENTS
;

Entities:
	Entity | Enum
;

Enum:
	'@enum' name=ID '{' features+=ID+ '}'
;
	
Entity:
	'@entity' name=ID '{' features+=Feature+ '}'
;
	
Feature:
	featureComment+=Comment* name=ID ':' type=FeatureType
;

FeatureType:
	EntitiesFeature | PrimitiveFeature | GenericListFeature
;

EntitiesFeature:
	type=[Entities|QualifiedName]
;

PrimitiveFeature:
	type=PrimitiveType
;

GenericListFeature:
	'@List' '<' type=FeatureType '>'
;

enum PrimitiveType:
	String='@String' | Short='@Short' | Integer='@Integer' | Long='@Long' | Float='@Float' |
	Double='@Double' | Boolean='@Boolean' | Byte='@Byte' | Date='@Date' |Datetime='@Datetime' | 
	Timestamp='@Timestamp' | Decimal='@Decimal';
	
/************************ Service definition *************************/
Exceptions:
	'@exceptions' '{' exceptions+=Exception+ '}'
;
	
Exception:
	'@exception' name=ID '{'
		'@msg' ':' msg=STRING
	'}'
;

/************************ Service definition *************************/
Service:
	'@service' name=ID '{' operations+=Operation+ '}'
;

Operation:
	'@operation' name=ID '{'
		('@input' ':' '{'
			featuresInput+=Feature+
		'}')?
		('@output' ':' '{'
			featuresOutput+=Feature+
		'}')?
		('@exception' ':' exceptionts+=[Exception|QualifiedName] (','  exceptionts+=[Exception|QualifiedName])* )?
	'}'
;

/************************ Service definition *************************/
Event:
	'@event' '{' '}'
;


terminal COMMENTS  		: '#' '^'?('a'..'z'|'A'..'Z'|'_'|' ') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|' ')*;